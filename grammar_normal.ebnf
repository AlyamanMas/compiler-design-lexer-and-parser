# NOTE: this file is the grammar before removing left recursion and common prefixes
# NOTE: empty here is for the empty string or epsillon/Lambda. Basically meaning the rule can go into nothing
# NOTE: the following rules are tokens/terminals that come from the lexer defined in lexer.l: NUM, ID, if, else, while, int, float... and everything that does not have a production rule on the left handside of this file, and everything double-quoted
program ::= Program ID "{" declaration-list statement-list "}" "."
declaration-list ::= declaration-list declaration | declaration
declaration ::= var-declaration
var-declaration ::= type-specifier ID ";" | type-specifier ID "[" NUM "]" ";"
type-specifier ::= int | float
params ::= param-list | "void"
param-list ::= param-list "," param | param
param ::= type-specifier ID | type-specifier ID "[" "]"
compound-stmt ::= "{" statement-list "}"
statement-list ::= statement-list statement | empty
statement ::= assignment-stmt | compound-stmt | selection-stmt | iteration-stmt
selection-stmt ::= if "(" expression ")" statement | if "(" expression ")" statement else statement
iteration-stmt ::= while "(" expression ")" statement
assignment-stmt ::= var "=" expression
var ::= ID | ID "[" expression "]"
expression ::= expression relop additive-expression | additive-expression
relop ::= "<" | "<=" | ">" | ">=" | "==" | "!="
additive-expression ::= additive-expression addop term | term
addop ::= "+" | "-"
term ::= term mulop factor | factor
mulop ::= "*" | "/"
factor ::= "(" expression ")" | var | NUM
